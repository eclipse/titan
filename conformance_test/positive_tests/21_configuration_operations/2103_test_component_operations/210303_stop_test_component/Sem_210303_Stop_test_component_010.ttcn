/******************************************************************************
 * Copyright (c) 2000-2016 Ericsson Telecom AB
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   Adrien Kirjak â€“ initial implementation
 *
 ** @version  0.0.1
 ** @purpose  1:21.3.3, deactivation of defaults in alive components after explicit stop
 ** @verdict  pass accept, ttcn3verdict:pass
 ***************************************************/

// The following requirements are tested:
// In contrast to that, all active defaults are automatically deactivated when 
// the alive-type component is stopped. 

module Sem_210303_Stop_test_component_010 { 

    type port P message {
        inout integer;
    } with {extension "internal"}
    
	type component GeneralComp {
        timer tc_tmr := 1.0;
        port P p;
	}
    
    altstep a_receive() runs on GeneralComp {
        [] p.receive(integer:?) { setverdict(fail); }
    }
    
    function f_ptcBehaviour() runs on GeneralComp {
        activate(a_receive());
        p.send(1);
        p.receive(integer:?); // infinite blocking as MTC doesn't send anything
        setverdict(fail);
    }

    function f_ptcSecondBehaviour() runs on GeneralComp {
        tc_tmr.start;
        alt {
            [] tc_tmr.timeout { setverdict(pass); }
        }
    }

    
	testcase TC_Sem_210303_Stop_test_component_010() runs on GeneralComp system GeneralComp {
        var GeneralComp v_ptc := GeneralComp.create("PTC") alive;
        connect(self:p, v_ptc:p);
        v_ptc.start(f_ptcBehaviour());
        p.receive(integer:?);
        v_ptc.stop;
        p.send(2);
        v_ptc.start(f_ptcSecondBehaviour());
        v_ptc.done;
		setverdict(pass);
	}
	
	control{
	    execute(TC_Sem_210303_Stop_test_component_010());
	}
}
